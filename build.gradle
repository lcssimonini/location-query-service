import org.springframework.cloud.contract.verifier.config.TestFramework

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.springframework.cloud:spring-cloud-contract-gradle-plugin:3.0.3'
    }
}

plugins {
    id 'org.springframework.boot' version '2.5.0'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id "jacoco"
    id "com.diffplug.spotless" version "5.12.5"
    id "org.sonarqube" version "3.2.0"
    id 'maven-publish'
}

apply plugin: 'spring-cloud-contract'


group = 'com.simo'
version = '0.0.1-SNAPSHOT'

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://repo.spring.io/snapshot' }
    maven { url 'https://repo.spring.io/milestone' }
}

ext {
    set('springCloudVersion', "2020.0.3")
    set('testcontainersVersion', "1.15.3")
}


dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
    implementation group: 'io.springfox', name: 'springfox-boot-starter', version: '3.0.0'
    implementation group: 'io.springfox', name: 'springfox-swagger-ui', version: '3.0.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'



    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.springframework.cloud:spring-cloud-starter-contract-verifier'
}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

contracts {
    testFramework = TestFramework.JUNIT5
    baseClassForTests = 'com.simo.locationqueryservice.util.contract.ControllerBaseTest'
}

test {
    useJUnitPlatform()
}

spotless {
    java {
        importOrder()
        removeUnusedImports()
        googleJavaFormat()
        indentWithSpaces()
        trimTrailingWhitespace()
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'com.simo'
            artifactId = 'locationqueryservice'
            version = '0.0.1-SNAPSHOT'
            from components.java
            artifact verifierStubsJar {
                classifier 'stubs'
            }
        }
    }
}

build.dependsOn(spotlessApply)

contractTest {
    useJUnitPlatform()
}

test {
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
}

sonarqube {
    properties {
        property "sonar.projectKey", "lcssimonini_location-query-service"
        property "sonar.organization", "lcssimonini"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}



